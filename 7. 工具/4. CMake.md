# CMake
## 1. 定义
CMake是开源、跨平台的编译工具，可以通过简单的配置文件去生成**本地的Makefile**，Makefile关系到整个工程的编译规则， 这个配置文件是**独立于运行平台和编译器**的，所以不用亲自去编写Makefile，而且配置文件可以拿到其它平台上使用
## 2. 使用
> 创建CMakeLists.txt文件
> 一般创建build文件夹，进入后运行命令
> $cmake ..
> $ make
> 生成的文件就会保留在build中
```shell
#****************************This is Project Info****************************
PROJECT(ProJ)	# 文件名
CMAKE_MINIMUM_REQUIRED(VERSION 2.8.12)	#最低版本号要求

#****************************This is Include File/Directories****************************
# 添加编译器用于查找头文件的文件夹
# ${CMAKE_CURRENT_SOURCE_DIR}这是当前处理的CMakeLists.txt所在的目录
INCLUDE_DIRECTORIES(${CMAKE_CURRENT_SOURCE_DIR}/include/)
#****************************This is Source File/Directories****************************
AUX_SOURCE_DIRECTORY(./src source_src)		#查找当前目录下的所有源文件，将所有的源文件存储在source_src变量中
SET(DIR_SRCS ${source_src})	   				

#****************************This is Library Directories****************************
LINK_DIRECTORIES(${CMAKE_CURRENT_SOURCE_DIR}/lib/)	#添加需要链接的库文件目录

#****************************This is 3Party Library****************************
LINK_LIBRARIES(pthread)				#添加需要链接的库文件目录

#****************************This is Output****************************

ADD_EXECUTABLE(${PROJECT_NAME} ${DIR_SRCS})			#指定生成目标
TARGET_LINK_LIBRARIES(${PROJECT_NAME})				#设置要链接的库文件的名称
```
## 3. 对makefile文件进行分析
### 1. 最低版本要求
表示cmake的==最低版本要求==是2.8
```bash
 cmake_minimum_required(VERSION 2.8)
```
### 2. 工程名
==表示本工程信息==，demo是工程名，可修改工程名

```bash
project（demo）
```

### 3. 生成可执行文件
表示最终==生成的elf文件==的名字叫main，==使用的源文件==是test1.c, test2.c,可以有多个文件

```bash
add_executable(main test1.c test2.c)
```

### 4. 将所有源文件存储在变量中
**==把指定目录下所有的源文件存储在一个变量中==**
第一个参数dir是指定目录，第二个参数var是用于存放源文件列表的变量

```bash
aux_source_directory(dir var)
```

aux_source_directory()也存在弊端，它会把指定目录下的所有源文件都加进来，可能会加入一些我们不需要的文件，此时我们可以使用set命令去新建变量来存放需要的源文件，如下，

```bash
set( SRC_LIST
	 ./main.c
	 ./testFunc1.c
	 ./testFunc.c)

add_executable(main ${SRC_LIST})
```

### 5. 指定头文件目录
该命令是**用来向工程==添加多个指定头文件的搜索路径==**，路径之间用空格分隔。

```bash
include_directories(test_func  test_func1)
```

其中test_func，test_func1是文件名

当然，也可以在main.c里使用include来指定路径，如下

```cpp
#include "test_func/testFunc.h"
#include "test_func1/testFunc1.h"
```

### 6. 添加存放源文件的子目录
这个命令可以向当前工程==添加存放源文件的子目录==，并可以指定中间二进制和目标二进制的存放位置

```cpp
 add_subdirectory(src)
```

### 7. 定义变量，设置存放路径

set命令是用来==定义变量==的


EXECUTABLE_OUT_PATH和PROJECT_SOURCE_DIR是CMake自带的预定义变量，其意义如下，
- EXECUTABLE_OUTPUT_PATH ：目标二进制可执行文件的存放位置
- PROJECT_SOURCE_DIR：工程的根目录
```bash
# 把存放elf文件的位置设置为工程根目录下的bin目录。
set (EXECUTABLE_OUTPUT_PATH ${PROJECT_SOURCE_DIR}/bin)
```

- LIBRARY_OUTPUT_PATH: 库文件的默认输出路径，这里设置为工程目录下的lib目录
```bash
set (LIBRARY_OUTPUT_PATH ${PROJECT_SOURCE_DIR}/lib)
```


### 8. 生成动（静）态库
==生成动态库或静态库==(第1个参数指定库的名字；第2个参数决定是动态还是静态，如果没有就默认静态；第3个参数指定生成库的源文件)
```cpp
add_library (testFunc_shared SHARED ${SRC_LIST})
add_library (testFunc_static STATIC ${SRC_LIST})
```
### 9. 设置最终生成的库名称
==设置最终生成的库的名称==，还有其它功能，如设置库的版本号等等

```cpp
set_target_properties (testFunc_shared PROPERTIES OUTPUT_NAME "testFunc")
set_target_properties (testFunc_static PROPERTIES OUTPUT_NAME "testFunc")
```
### 10. 查找指定库
==在指定目录下查找指定库，并把库的绝对路径存放到变量里==，其第一个参数是变量名称，第二个参数是库名称，第三个参数是HINTS，第4个参数是路径，其它用法可以参考cmake文档

```bash
find_library(TESTFUNC_LIB testFunc HINTS ${PROJECT_SOURCE_DIR}/testFunc/lib)
```
使用find_library的好处是在执行cmake ..时就会去查找库是否存在，这样可以提前发现错误，不用等到链接时

ps：在lib目录下有testFunc的静态库和动态库，`find_library(TESTFUNC_LIB testFunc ..`.默认是查找动态库，如果想直接指定使用动态库还是静态库，可以写成`find_library(TESTFUNC_LIB libtestFunc.so ...`或者`find_library(TESTFUNC_LIB libtestFunc.a ...`

ps： 查看elf文件使用了哪些库，可以使用readelf -d ./xx来查看
### 11. 链接库
==把目标文件与库文件进行链接==

```bash
target_link_libraries (main ${TESTFUNC_LIB})
```
### 12. 添加编译选项
有时编译程序时想添加一些编译选项，如-Wall，-std=c++11等，就可以使用add_compile_options来进行操作。

```cpp
add_compile_options(-std=c++11 -Wall) 
```
### 13. 添加控制选项
有时希望在编译代码时只编译一些指定的源码，可以使用cmake的option命令，主要遇到的情况分为2种：
1. 本来要生成多个bin或库文件，现在只想生成部分指定的bin或库文件
2. 对于同一个bin文件，只想编译其中部分代码（使用宏来控制）

#### 第一种情况
假设我们现在的工程会生成2个bin文件，main1和main2。
外层的CMakeLists.txt内容如下，

```cpp
cmake_minimum_required(VERSION 3.5)

project(demo)

option(MYDEBUG "enable debug compilation" OFF)

set (EXECUTABLE_OUTPUT_PATH ${PROJECT_SOURCE_DIR}/bin)

add_subdirectory(src)
```
这里使用了option命令，其第一个参数是这个option的名字，第二个参数是字符串，用来描述这个option是来干嘛的，第三个是option的值，ON或OFF，也可以不写，不写就是默认OFF。

然后编写src目录下的CMakeLists.txt，如下

```cpp
cmake_minimum_required (VERSION 3.5)

add_executable(main1 main1.c)

if (MYDEBUG)
    add_executable(main2 main2.c)
else()
    message(STATUS "Currently is not in debug mode")    
endif()

```
注意，这里使用了if-else来根据option来决定是否编译main2.c

然后cd到build目录下输入`cmake .. && make`就可以只编译出main1，如果想编译出main2，就把MYDEBUG设置为ON，再次输入`cmake .. && make`重新编译。

#### 第二种情况
假设我们有个main.c，其内容如下，

```cpp
#include <stdio.h>

int main(void)
{
#ifdef WWW1
    printf("hello world1\n");
#endif    

#ifdef WWW2     
    printf("hello world2\n");
#endif

    return 0;
}
```
可以通过定义宏来控制打印的信息，我们CMakeLists.txt内容如下，

```cpp
cmake_minimum_required(VERSION 3.5)

project(demo)

set (EXECUTABLE_OUTPUT_PATH ${PROJECT_SOURCE_DIR}/bin)

option(WWW1 "print one message" OFF)
option(WWW2 "print another message" OFF)

if (WWW1)
    add_compile_options(-DWWW1)
endif()

if (WWW2)
    add_compile_options(-DWWW2)
endif()

add_executable(main main.c)

```
这里把option的名字保持和main.c里的宏名称一致，这样更加直观，也可以选择不同的名字。通过与add_compile_options()的配合，就可以控制单个bin文件的打印输出了。
cd到build目录下执行`cmake .. && make`，然后到bin目录下执行`./main`，可以看到打印为空，
接着分别按照下面指令去执行，然后查看打印效果，

- `cmake .. -DWWW1=ON -DWWW2=OFF && make`
- `cmake .. -DWWW1=OFF -DWWW2=ON && make`
- `cmake .. -DWWW1=ON -DWWW2=ON && make`

如果option有变化，要么删除上次执行cmake时产生的缓存文件，要么把所有的option都显式的指定其值。

## 4. 平时碰到的命令
### find_package()
引入外部依赖包

```bash
# OpenCV_DIR为OpenCV库的配置文件所在路径
set(OpenCV_DIR ${PROJECT_SOURCE_DIR}/lib/libopencv/arm/share/OpenCV)
# 引入外部依赖包
find_package(OpenCV 3 REQUIRED)	
# 设置
include_directories(${OpenCV_INCLUDE_DIRS})

# 设置
message(STATUS "Project: ${PROJECT_NAME}")
message(STATUS "OpenCV library status:")
message(STATUS "version: ${OpenCV_VERSION}")
message(STATUS "libraries: ${OpenCV_LIBS}")
message(STATUS "include path: ${OpenCV_INCLUDE_DIRS}")
```
### cmake使用protobuf生成C++代码

```bash
set(PROTO_FILES ${PROJECT_SOURCE_DIR}/protofile/message.proto)

# 将message.proto文件生成C++源码，使用PROTO_SRC,PROTO_HARS变量分别指代生成的cpp和h文件并可用于连接到target和设置include
protobuf_generate_cpp(PROTO_SRCS PROTO_HDRS ${PROTO_FILES}) 
```
### 指定第三方库所在路径

```bash
LINK_DIRECTORIES (/home/myproject/libs)
```

# 参考文章
- [Linux下CMake简明教程](https://blog.csdn.net/whahu1989/article/details/82078563)                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                            